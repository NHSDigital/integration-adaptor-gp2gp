plugins {
	id 'org.springframework.boot' version '2.7.18'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "checkstyle"
	id "com.github.spotbugs" version "5.0.12"
	id "io.freefair.lombok" version "6.6.3"
	id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"
apply plugin: 'application'

mainClassName = ' uk.nhs.adaptors.gp2gp.Gp2gpApplication'

group = 'uk.nhs.adaptors'
sourceCompatibility = '17'

checkstyle {
	toolVersion '8.38'
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework:spring-jms'

	implementation 'org.yaml:snakeyaml:1.33'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'

	// WebClient
	implementation ('org.springframework.boot:spring-boot-starter-webflux')
	implementation('org.projectreactor:reactor-spring:1.0.1.RELEASE') {
		exclude group: 'com.jayway.jsonpath', module: 'json-path'
	}
	implementation('com.jayway.jsonpath:json-path:2.5.0') {
		exclude group: 'net.minidev', module: 'json-smart'
	}
	implementation('net.minidev:json-smart:2.4.7')

	// Logging
	implementation 'ch.qos.logback:logback-classic:1.2.9'

	// Infrastructure
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.279'
	implementation ('com.azure:azure-storage-blob:12.18.0')
	implementation ('org.apache.qpid:qpid-jms-client:1.2.0')

	// Utils
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'javax.xml.soap:javax.xml.soap-api:1.4.0'
	implementation 'com.github.spullara.mustache.java:compiler:0.9.11'
	implementation 'org.apache.tika:tika-core:1.28.4'

	// Fhir
	implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:5.4.0'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.assertj:assertj-core:3.24.2"
	testImplementation ("org.testcontainers:testcontainers:1.16.3") {
		exclude group: 'org.apache.commons', module: 'commons-compress'
	}
	testImplementation 'org.awaitility:awaitility:4.2.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
	testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.27.2'
	testImplementation 'com.squareup.okhttp3:okhttp:4.10.0'
	testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

	// upgraded to fix SNYK-JAVA-ORGAPACHECOMMONS-3043138
	spotbugs 'com.github.spotbugs:spotbugs:4.7.3'

	constraints {
		implementation('commons-io:commons-io:2.7') {
			because 'to fix https://snyk.io/vuln/SNYK-JAVA-COMMONSIO-1277109'
		}
		implementation('org.apache.commons:commons-compress:1.21') {
			because 'to fix SNYK-JAVA-ORGAPACHECOMMONS-1316638'
		}
		implementation('com.azure:azure-core-http-netty:1.9.1') {
			because 'to fix SNYK-JAVA-IONETTY-1083991'
		}
		implementation('org.apache.commons:commons-text:1.10.0') {
			because 'to fix SNYK-JAVA-ORGAPACHECOMMONS-3043138'
		}
		implementation('org.yaml:snakeyaml:1.33') {
			because 'to fix SNYK-JAVA-ORGYAML-3016891, SNYK-JAVA-ORGYAML-3016889, SNYK-JAVA-ORGYAML-3016888, SNYK-JAVA-ORGYAML-2806360'
		}
		implementation('com.fasterxml.jackson.core:jackson-databind:2.13.4.2') {
			because 'to fix SNYK-JAVA-COMFASTERXMLJACKSONCORE-3038426'
		}
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/intTest/java')
		}

		resources {
			srcDir file('src/intTest/resources')
		}
	}
}

task(interoperabilityTestingToolJsonToXml, dependsOn: 'classes', type: JavaExec) {
	mainClass.set('uk.nhs.adaptors.gp2gp.transformjsontoxmltool.TransformJsonToXml')
	classpath = sourceSets.main.runtimeClasspath
}

configurations {
	integrationTestCompileOnly.extendsFrom testCompileOnly
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

task integrationTest(type: Test) {
	useJUnitPlatform() {
		description = 'Runs integration tests.'
		group = 'verification'

		testClassesDirs = sourceSets.integrationTest.output.classesDirs
		classpath = sourceSets.integrationTest.runtimeClasspath
		shouldRunAfter test
	}
}

tasks.withType(Test) {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

check.dependsOn integrationTest
jacocoTestReport.dependsOn integrationTest

spotbugsTest.enabled = false
spotbugsIntegrationTest.enabled = false
spotbugsMain {
	reports {
		html {
			enabled = true
		}
		xml {
			enabled = true
		}
		excludeFilter.set(file("config/spotbugs/exclude.xml"))
	}
}

bootJar {
	exclude("**/TransformJsonToXml*")
}