plugins {
	id 'org.springframework.boot' version '2.5.12'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "checkstyle"
	id "com.github.spotbugs" version "4.5.0"
	id "io.freefair.lombok" version "5.3.0"
	id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"
apply plugin: 'application'

mainClassName = ' uk.nhs.adaptors.gp2gp.Gp2gpApplication'

group = 'uk.nhs.adaptors'
sourceCompatibility = '11'

lombok {
	config['lombok.log.fieldName'] = 'LOGGER'
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework:spring-jms:5.2.6.RELEASE'

	// WebClient
	implementation ('org.springframework.boot:spring-boot-starter-webflux:2.5.5') {
		exclude group: 'io.netty', module: 'netty-codec'
	}
	implementation('org.projectreactor:reactor-spring:1.0.1.RELEASE') {
		exclude group: 'com.jayway.jsonpath', module: 'json-path'
	}
	implementation('com.jayway.jsonpath:json-path:2.5.0') {
		exclude group: 'net.minidev', module: 'json-smart'
	}
	implementation('net.minidev:json-smart:2.4.7')

	//Keystore
	implementation 'com.heroku.sdk:env-keystore:1.1.6'

	// Logging
	implementation 'ch.qos.logback:logback-classic:1.2.3'

	// Infrastructure
	implementation 'com.amazonaws:aws-java-sdk-s3:1.11.955'
	implementation ('com.azure:azure-storage-blob:12.10.0') {
		exclude group: 'io.netty', module: 'netty-codec'
	}
	implementation ('org.apache.qpid:qpid-jms-client:1.2.0') {
		exclude group: 'io.netty', module: 'netty-codec-http'
	}

	// Utils
	implementation 'org.apache.commons:commons-lang3:3.11'
	implementation 'javax.xml.soap:javax.xml.soap-api:1.4.0'
	implementation 'com.github.spullara.mustache.java:compiler:0.9.5'
	implementation 'org.apache.tika:tika-core:1.26'

	// Fhir
	implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:5.4.0'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.assertj:assertj-core:3.18.1"
	testImplementation ("org.testcontainers:testcontainers:1.15.1") {
		exclude group: 'org.apache.commons', module: 'commons-compress'
	}
	testImplementation 'org.awaitility:awaitility:4.0.3'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
	testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.27.2'

	constraints {
		implementation('org.bouncycastle:bcprov-jdk15on:1.68') {
			because 'env-keystore:1.1.6 depends on previous version with CVE-2020-28052'
		}
		implementation('com.google.guava:guava:30.1-jre') {
			because 'hapi-fhir-structures-dstu3:5.2.1 depends on previous version with CVE-2020-8908'
		}
		implementation('commons-codec:commons-codec:1.15') {
			because 'hapi-fhir-structures-dstu3:5.2.1 depends on previous version with SNYK-JAVA-COMMONSCODEC-561518'
		}
		implementation('commons-io:commons-io:2.7') {
			because 'to fix https://snyk.io/vuln/SNYK-JAVA-COMMONSIO-1277109'
		}
		implementation('io.netty:netty-codec-http:4.1.68.Final') {
			because 'to fix SNYK-JAVA-IONETTY-1070799'
		}
		implementation('io.netty:netty-codec:4.1.68.Final') {
			because 'to fix SNYK-JAVA-IONETTY-1584064'
		}
		implementation('org.apache.commons:commons-compress:1.21') {
			because 'to fix SNYK-JAVA-ORGAPACHECOMMONS-1316638'
		}
		implementation('com.azure:azure-core-http-netty:1.9.1') {
			because 'to fix SNYK-JAVA-IONETTY-1083991'
		}
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/intTest/java')
		}

		resources {
			srcDir file('src/intTest/resources')
		}
	}
}

task(interoperabilityTestingToolJsonToXml, dependsOn: 'classes', type: JavaExec) {
	main = 'uk.nhs.adaptors.gp2gp.transformjsontoxmltool.TransformJsonToXml'
	classpath = sourceSets.main.runtimeClasspath
}

configurations {
	integrationTestCompileOnly.extendsFrom testCompileOnly
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

task integrationTest(type: Test) {
	useJUnitPlatform() {
		description = 'Runs integration tests.'
		group = 'verification'

		testClassesDirs = sourceSets.integrationTest.output.classesDirs
		classpath = sourceSets.integrationTest.runtimeClasspath
		shouldRunAfter test
	}
}

tasks.withType(Test) {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

check.dependsOn integrationTest
jacocoTestReport.dependsOn integrationTest

spotbugsTest.enabled = false
spotbugsIntegrationTest.enabled = false
spotbugsMain {
	reports {
		html {
			enabled = true
		}
		xml {
			enabled = true
		}
	}
}

bootJar {
	exclude("**/TransformJsonToXml*")
}